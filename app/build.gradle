buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }

}

apply plugin: 'com.android.application'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://github.com/uPhyca/stetho-realm/raw/master/maven-repo' }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'
apply plugin: 'realm-android'

android {

    dexOptions {
        maxProcessCount 2
        javaMaxHeapSize "2g"
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    def config = rootProject.extensions.getByName("ext")

    compileSdkVersion config["compileSdkVersion"]
    buildToolsVersion config["buildToolsVersion"]

    defaultConfig {
        applicationId config["applicationId"]
        minSdkVersion config["minSdkVersion"]
        targetSdkVersion config["targetSdkVersion"]
        versionCode config["versionCode"]
        versionName config["versionName"]
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    dexOptions { jumboMode = true }

    buildTypes {
        debug {
            minifyEnabled true
            applicationIdSuffix config["debugVersionNameSuffix"]
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        test.java.srcDirs += "src/test/kotlin"
        androidTest.java.srcDirs += "src/androidTest/kotlin"

        main {
            res.srcDirs += "src/main/res-main"
            file("src/main/res-screen")
                    .listFiles()
                    .each { res.srcDirs += it.path }
        }
    }
}

retrolambda {
    javaVersion JavaVersion.VERSION_1_7
    defaultMethods false
    incremental true
}

dependencies {

    // Library, Apt versions
    def daggerVersion = '2.6.1'
    def lombokVersion = '1.16.10'
    def appcompatVersion = '25.3.1'
    def retrofitVersion = '2.1.0'
    def okHttpVersion = '3.4.1'
    def playServicesVersion = '10.0.1'

    //Testing Library, Apt Versions
    def kotlinVersion = '1.0.3'
    def mockitoVersion = '1.10.19'
    def assertJVersion = '3.5.1'

    //------------------------------------ dependencies --------------------------------------------
    //apt
    apt "com.google.dagger:dagger-compiler:${daggerVersion}"
    apt "org.projectlombok:lombok:${lombokVersion}"
    apt 'com.jakewharton:butterknife-compiler:8.5.1'

    //provided
    provided "org.projectlombok:lombok:${lombokVersion}"
    provided "javax.annotation:jsr250-api:1.0"

    //main
    compile "com.agna.ferro:ferro-mvp-rx:1.0.2"
    compile "com.google.dagger:dagger:${daggerVersion}"
    compile "com.android.support:appcompat-v7:${appcompatVersion}"
    compile "com.android.support:design:${appcompatVersion}"
    compile "com.android.support:recyclerview-v7:${appcompatVersion}"
    compile "com.android.support:cardview-v7:${appcompatVersion}"
    compile "com.android.support:palette-v7:${appcompatVersion}"
    compile "com.android.support:customtabs:${appcompatVersion}"
    compile "com.android.support:percent:${appcompatVersion}"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile "com.android.support:multidex:1.0.1"
    compile "io.reactivex:rxjava:1.1.7"
    compile "io.reactivex:rxandroid:1.2.1"
    compile "javax.inject:javax.inject:1"
    compile "com.github.bumptech.glide:glide:3.7.0"
    compile "com.annimon:stream:1.1.0"
    compile "com.jakewharton.rxbinding:rxbinding:0.4.0"
    compile "com.intellij:annotations:12.0"
    compile 'com.jakewharton:butterknife:8.5.1'

    //network
    compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    compile "com.squareup.retrofit2:adapter-rxjava:${retrofitVersion}"
    compile "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
    compile "com.squareup.okhttp3:logging-interceptor:${okHttpVersion}"
    compile "com.google.code.gson:gson:2.7"

    //logging
    compile "com.jakewharton.timber:timber:4.1.2"
    compile "com.github.anrwatchdog:anrwatchdog:1.2.0"
    compile("com.crashlytics.sdk.android:crashlytics:2.6.5@aar") {
        transitive = true;
    }

    //play services
    compile "com.google.android.gms:play-services-location:${playServicesVersion}"
    compile "com.google.android.gms:play-services-maps:${playServicesVersion}"
    compile "com.google.android.gms:play-services-plus:${playServicesVersion}"
    compile "com.google.android.gms:play-services-auth:${playServicesVersion}"
    compile "com.google.android.gms:play-services-analytics:${playServicesVersion}"

    //other
    compile 'com.github.chrisbanes:PhotoView:1.3.0'
    compile 'com.wdullaer:materialdatetimepicker:2.5.0'
    compile 'me.relex:circleindicator:1.2.2@aar'
    compile 'me.zhanghai.android.materialprogressbar:library:1.3.0'
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.3'
    compile 'uk.co.chrisjenx:calligraphy:2.2.0'
    compile 'com.yayandroid:ParallaxRecyclerView:1.1'
    compile 'konifar:fab-transformation:1.0.0'
    compile 'com.facebook.stetho:stetho:1.5.0'
    compile 'com.uphyca:stetho_realm:2.0.0'
    compile 'com.google.android:flexbox:0.2.6'

    //--------------------------------- test dependencies ------------------------------------------
    //unit test
    testApt "com.google.dagger:dagger-compiler:${daggerVersion}"
    testCompile "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:${mockitoVersion}"
    testCompile "org.assertj:assertj-core:${assertJVersion}"

    //instrumental test
    androidTestApt "com.google.dagger:dagger-compiler:${daggerVersion}"
    androidTestCompile "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"

    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.2'
    androidTestCompile "com.android.support:support-annotations:${appcompatVersion}"
    androidTestCompile('com.android.support.test:runner:0.5')

    //debug
    debugCompile "com.github.brianPlummer:tinydancer:0.1.0"

}

apply plugin: 'com.google.gms.google-services'
